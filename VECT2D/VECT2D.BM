''
' QB64_GJ_LIB
' GRYMMJACK'S VECT2D LIB
'
' 2D Vector support for QB64
'
' USAGE:
'   Insert '$INCLUDE:'path_to_GJ_LIB/VECT2D/VECT2D.BI' at the top of file
'   Insert '$INCLUDE:'path_to_GJ_LIB/VECT2D/VECT2D.BM' at the bottom of file
' 
' @author Rick Christy <grymmjack@gmail.com>
' @author Evan Shortiss <https://github.com/evanshortiss/vector2d>
' @support William Barnes
'
$INCLUDEONCE

''
' Sets both x and y axes of the vector
'
' @param VECT2D vret Vector with both axes set
' @param SINGLE x x axis
' @param SINGLE y y axis
' @return VECT2D inside vret
'
SUB VECT2D.setAxes(vret AS VECT2D, x AS SINGLE, y AS SINGLE)
    vret.x! = x!
    vret.y! = y!
END SUB


''
' Sets x axis of the vector
'
' @param VECT2D vret Vector with x axis set
' @param SINGLE x x axis
' @return VECT2D inside vret
'
SUB VECT2D.setX(vret AS VECT2D, x AS SINGLE)
    vret.x! = x!
END SUB


''
' Sets y axis of the vector
'
' @param VECT2D vret Vector with y axis set
' @param SINGLE y y axis
' @return VECT2D inside vret
'
SUB VECT2D.setY(vret AS VECT2D, y AS SINGLE)
    vret.y! = y!
END SUB


''
' Returns VECT2D as a string
'
' @param VECT2D vec1 Vector to return as string
' @param INTEGER (TRUE/FALSE) round x and y?
' @return STRING representation of VECT2D
'
FUNCTION VECT2D$(vec1 AS VECT2D, rounded AS INTEGER)
    IF rounded% = FALSE THEN
        VECT2D$ = "(" _
            + _TRIM$(STR$(vec1.x!)) _
            + ", " + _TRIM$(STR$(vec1.y!)) _
        + ")"
    ELSE
        DIM vret AS VECT2D
        VECT2D.round vret, vec1
        VECT2D$ = "(" _
            + _TRIM$(STR$(vret.x!)) _
            + ", " + _TRIM$(STR$(vret.y!)) _
        + ")"
    END IF
END FUNCTION


''
' Get x axis of VECT2D
'
' @param VECT2D vec1 Vector to get x axis for
' @return SINGLE x axis
'
FUNCTION VECT2D.getX!(vec1 AS VECT2D)
    VECT2D.getX! = vec1.x!
END FUNCTION



''
' Get y axis of VECT2D
'
' @param VECT2D vec1 Vector to get y axis for
' @return SINGLE y axis
'
FUNCTION VECT2D.getY!(vec1 AS VECT2D)
    VECT2D.getY! = vec1.y!
END FUNCTION


''
' Add two VECT2D axes together
'
' @param VECT2D vret Return vector with result of addition
' @param VECT2D vec1 Left VECT2D operand
' @param VECT2D vec2 Right VECT2D operand
' @return VECT2D inside vret
' 
SUB VECT2D.add(vret AS VECT2D, vec1 AS VECT2D, vec2 AS VECT2D)
    vret.x! = vec1.x! + vec2.x!
    vret.y! = vec1.y! + vec2.y!
END SUB


''
' Subtract two VECT2D axes from each other
'
' @param VECT2D vret Return vector with result of subtraction
' @param VECT2D vec1 Left VECT2D operand
' @param VECT2D vec2 Right VECT2D operand
' @return VECT2D inside vret
' 
SUB VECT2D.sub(vret AS VECT2D, vec1 AS VECT2D, vec2 AS VECT2D)
    vret.x! = vec1.x! - vec2.x!
    vret.y! = vec1.y! - vec2.y!
END SUB


''
' Multiply two VECT2D axes together
'
' @param VECT2D vret Return vector with result of mulitplication
' @param VECT2D vec1 Left VECT2D operand
' @param VECT2D vec2 Right VECT2D operand
' @return VECT2D inside vret
' 
SUB VECT2D.multByVECT2D(vret AS VECT2D, vec1 AS VECT2D, vec2 AS VECT2D)
    vret.x! = vec1.x! * vec2.x!
    vret.y! = vec1.y! * vec2.y!
END SUB


''
' Multiply VECT2D axes by a single number
'
' @param VECT2D vret Return vector with result of mulitplication
' @param VECT2D vec1 VECT2D to multiply axes of
' @param SINGLE n Number to mulitply by
' @return VECT2D inside vret
' 
SUB VECT2D.multBySingle(vret AS VECT2D, vec1 AS VECT2D, n AS single)
    vret.x! = vec1.x! * n!
    vret.y! = vec1.y! * n!
END SUB


''
' Divide two VECT2D axes from each other
'
' @param VECT2D vret Return vector with result of division
' @param VECT2D vec1 Left VECT2D operand
' @param VECT2D vec2 Right VECT2D operand
' @return VECT2D inside vret
' 
SUB VECT2D.divByVECT2D(vret AS VECT2D, vec1 AS VECT2D, vec2 AS VECT2D)
    vret.x! = vec1.x! / vec2.x!
    vret.y! = vec1.y! / vec2.y!
END SUB


''
' Divide VECT2D axes by a single number
'
' @param VECT2D vret Return vector with result of division
' @param VECT2D vec1 VECT2D to divide axes of
' @param SINGLE n Number to divide by
' @return VECT2D inside vret
' 
SUB VECT2D.divBySingle(vret AS VECT2D, vec1 AS VECT2D, n AS SINGLE)
    vret.x! = vec1.x! / n!
    vret.y! = vec1.y! / n!
END SUB


''
' Normalize a VECT2D into a unit vector
'
' @param VECT2D vret Return vector normalized
' @param VECT2D vec1 Vector to normalize
' @return VECT2D inside vret
' 
SUB VECT2D.normalize(vret AS VECT2D, vec1 AS VECT2D)
    DIM magnitude AS SINGLE
    magnitude! = VECT2D.magnitude(vec1.x!, vec1.y!)
    VECT2D.divBySingle vret, vec1, magnitude!
END SUB


''
' Normalize a VECT2D into a unit vector (alias)
'
' @param VECT2D vret Return vector normalized
' @param VECT2D vec1 Vector to normalize
' @return VECT2D inside vret
' 
SUB VECT2D.unit(vret AS VECT2D, vec1 AS VECT2D)
    VECT2D.normalize vret, vec1
END SUB


''
' Reverse both VECT2D axes (invert sign)
'
' @param VECT2D vret Return vector with axes reversed/inverted
' @param VECT2D vec1 Vector to reverse/invert
' @return VECT2D inside vret
' 
SUB VECT2D.reverse(vret AS VECT2D, vec1 AS VECT2D)
    vret.x! = -vec1.x!
    vret.y! = -vec1.y!
END SUB


''
' Get absolute values for VECT2D axes (ignore sign)
'
' @param VECT2D vret Return vector with unsigned axes 
' @param VECT2D vec1 Vector to get axes for
' @return VECT2D inside vret
' 
SUB VECT2D.abs(vret AS VECT2D, vec1 AS VECT2D)
    vret.x! = ABS(vec1.x!)
    vret.y! = ABS(vec1.y!)
END SUB


''
' Set both VECT2D axes to 0
'
' @param VECT2D vret Return vector with zeroed axes
' @return VECT2D inside vret
' 
SUB VECT2D.zero(vret AS VECT2D)
    vret.x! = 0
    vret.y! = 0
END SUB


''
' Get distance between two VECT2Ds
'
' @param VECT2D vec1 Vector to measure distance from
' @param VECT2D vec2 Vector to measure distance to
' @return SINGLE distance between the vectors
' 
FUNCTION VECT2D.distance!(vec1 AS VECT2D, vec2 AS VECT2D)
    DIM AS SINGLE x, y
    x! = vec1.x! - vec2.x!
    y! = vec1.y! - vec2.y!
    VECT2D.distance! = SQR(x! * x! + y! * y!)
END FUNCTION


''
' Rotate a vector by radians
'
' @param VECT2D vret Return vector with rotated axes
' @param VECT2D vec1 Vectore to rotate axes of
' @param SINGLE radians Radians to rotate vector by
' @return VECT2D inside vret
'
SUB VECT2D.rotate(vret as VECT2D, vec1 AS VECT2D, radians as SINGLE)
    DIM AS SINGLE cosine, sine
    cosine! = COS(radians)
    sine!   = SIN(radians)
    vret.x! = vec1.x! * cosine! - vec1.y! * sine!
    vret.y! = vec1.x! * sine! + vec1.y! * cosine!
END SUB


''
' Round the axes of a vector
'
' @param VECT2D vret Return vector with rounded axes
' @param VECT2D vec1 Vector to round axes for
' @return VECT2D inside vret
' 
SUB VECT2D.round(vret AS VECT2D, vec1 AS VECT2D)
    vret.x! = _ROUND(vec1.x!)
    vret.y! = _ROUND(vec1.y!)
END SUB


''
' Return length squared of vector
'
' @param VECT2D vec1 Vector to operate on
' @return SINGLE length squared of vector
' 
FUNCTION VECT2D.lengthsq!(vec1 AS VECT2D)
    VECT2D.lengthsq! = vec1.x! * vec1.x! + vec1.y! * vec1.y!
END FUNCTION


''
' Return length(magnitude) of vector
'
' @param VECT2D vec1 Vector to get length(magnitude) for
' @return SINGLE length of vector
' 
FUNCTION VECT2D.length!(vec1 AS VECT2D)
    VECT2D.length! = VECT2D.magnitude(vec1.x!, vec1.y!)
END FUNCTION


''
' Get dot product of two vectors
'
' @param VECT2D vec1 Left VECT2D operand
' @param VECT2D vec1 Right VECT2D operand
' @return SINGLE dot product of two vectors
'
FUNCTION VECT2D.dotproduct!(vec1 AS VECT2D, vec2 AS VECT2D)
    VECT2D.dotproduct! = vec1.x! * vec2.x! + vec1.y! * vec2.y!
END FUNCTION


''
' Get cross product of two vectors
'
' @param VECT2D vec1 Left VECT2D operand
' @param VECT2D vec1 Right VECT2D operand
' @return SINGLE cross product of two vectors
'
FUNCTION VECT2D.crossproduct!(vec1 AS VECT2D, vec2 AS VECT2D)
    VECT2D.crossproduct! = vec1.x! * vec2.y! - vec1.y! * vec2.x!
END FUNCTION


''
' Get magnitude(length) of vector
'
' @param SINGLE x axis of vector
' @param SINGLE y axis of vector
' @return SINGLE magnitude(length) of vector
' 
FUNCTION VECT2D.magnitude!(x AS SINGLE, y AS SINGLE)
    VECT2D.magnitude! = SQR(x * x + y * y)
END FUNCTION


''
' Check if two vectors have equal axes
'
' @param VECT2D vec1 Left VECT2D operand
' @param VECT2D vec2 Right VECT2D operand
' @return INTEGER (TRUE/FALSE) if vectors are equal
' 
FUNCTION VECT2D.eq%(vec1 AS VECT2D, vec2 AS VECT2D)
    IF vec1.x! = vec2.x! AND vec1.y! = vec2.y! THEN
        VECT2D.eq% = TRUE
    ELSE
        VECT2D.eq% = FALSE
    END IF
END FUNCTION


''
' Converts radians to degress (wrapper to _R2D)
'
' @param SINGLE radians to convert to degrees
' @return SINGLE degrees converted from radians
' 
FUNCTION VECT2D.radians_to_degrees!(radians AS SINGLE)
    VECT2D.radians_to_degrees! = _R2D(radians)
    ' Formula: radians_to_degrees! = radians! * 180 / _PI
END FUNCTION


''
' Converts degrees to radians (wrapper to _D2R)
'
' @param SINGLE radians to convert to degrees
' @return SINGLE radians converted from degrees
' 
FUNCTION VECT2D.degrees_to_radians!(degrees AS SINGLE)
    VECT2D.degrees_to_radians! = _D2R(degrees)
    ' Formula: degrees_to_radians! = degrees! * _PI / 180
END FUNCTION
