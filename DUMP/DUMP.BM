''
' QB64_GJ_LIB
' GRYMMJACK'S DUMP LIB
'
' Dumps variables in a human friendly way to assist in debugging.
' Inspired by PHP print_r() [1] which I missed when writing in QB64.
'
' So, why "dump"?
'
' [0] dump: 
'       to copy (data in a computer's internal storage) to an external storage 
'       or output device
'          ^^^^^^^^^^^^^ 
'               this
'
' USAGE:
'   Insert '$INCLUDE:'path_to_GJ_LIB/DUMP/DUMP.BI' at the top of file
'   Insert '$INCLUDE:'path_to_GJ_LIB/DUMP/DUMP.BM' at the bottom of file
'
' TL;DR: 
'
' Every function returns a string called a dump block. The idea is that
' You can then PRINT the string in your code where you need to see what vars
' have inside them quickly.
'
' A dump block consists of:
' - A blank line
' - The type of variable being dumped
' - The number of elements in an array (if applicable)
' - A label for reference
'
' Here is an example of a dump of a string array:
'
' DIM words$(2)
' words$(0) = "foo" : words$(1) = "bar" : words$(2) = "baz"
' PRINT dump_string_array(words$(), "words")
' 
' OUTPUT:
' 
' STRING ARRAY: words$(2) {
'   (0): "foo" [3]
'   (1): "bar" [3]
'   (2): "baz" [3]
' }
' 
' Because QB64 lacks any reflection support, has no way to pass an optional
' argument, has no ability to identify a variable type, or even eval() it's
' own dialect, there are separate functions for each common type. If you don't
' see the one you need, it's easy enough to copy an existing one and make what
' you want while staying in the spirit of DUMP LIB.
' 
' FUNCTION              NOTES
' dump_bit_array$                     Returns string with a dump of an array of bits
' dump_unsigned_bit_array$            Returns string with a dump of an array of unsigned bits
' dump_byte_array$                    Returns string with a dump of an array of bytes
' dump_unsigned_byte_array$           Returns string with a dump of an array of unsigned bytes
' dump_unsigned_integer$              Returns string with a dump of an array of unsigned integers
' dump_unsigned_byte_array_as_hex$    Returns string with a dump of an array of unsigned bytes as hex
' dump_unsigned_byte_array_as_ascii$  Returns string with a dump of an array of unsigned bytes as hex and ASCII
' dump_string$                        Includes handy output of the strings length.
' dump_string_array$                  Works on 1 dimensional arrays only (right now).
' dump_integer_array$                 Works on 1 dimensional arrays only (right now).
' dump_single_array$                  Works on 1 dimensional arrays only (right now).
' dump_long_array$                    Works on 1 dimensional arrays only (right now).
' dump_double_array$                  Works on 1 dimensional arrays only (right now).
' dump_dict$                          Dump a dictionary object and it's contents.
'
' @author Rick Christy <grymmjack@gmail.com>
' @depends DUMP.BI
' @see [0] https://www.merriam-webster.com/dictionary/dump
' @see [1] https://www.php.net/manual/en/function.print-r.php
'
$LET GJ_LIB_DUMP_INC_BM = 1



''
' Returns string with a dump of a string
' 
' @param s$ String to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_string$ (s$, label$)
    DIM AS STRING l, r
    l$ = _TRIM$(STR$(LEN(s$)))
    r$ = GJ_LIB_NL$ + "STRING: " + label$ + "$ {" + GJ_LIB_NL$
    r$ = r$ + "  " + CHR$(34) + s$ + CHR$(34) + " [" + l$ + "]" + GJ_LIB_NL$
    r$ = r$ + "} "
    dump_string$ = r$
END FUNCTION


''
' Returns string with a dump of an array of bits
'
' @param arr$() Array of bits to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_bit_array$ (arr`(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING t, r
    lb = LBOUND(arr`) : ub = UBOUND(arr`) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "STRING ARRAY: " + label$ + "`(" + t$ + ") {" + GJ_LIB_NL$
    FOR i = lb TO ub
        r$ = r$ + "  (" + _TRIM$(STR$(i)) + "): " + STR$(arr`(i))
        r$ = r$ + " [" + t$ + "]" + GJ_LIB_NL$
    NEXT i
    r$ = r$ + "}"
    dump_bit_array$ = r$
END FUNCTION


''
' Returns string with a dump of an array of unsigned bits
'
' @param arr$() Array of unsigned bits to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_unsigned_bit_array$ (arr~`(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING t, r
    lb = LBOUND(arr~`) : ub = UBOUND(arr~`) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "STRING ARRAY: " + label$ + "~`(" + t$ + ") {" + GJ_LIB_NL$
    FOR i = lb TO ub
        r$ = r$ + "  (" + _TRIM$(STR$(i)) + "): " + STR$(arr~`(i))
        r$ = r$ + " [" + t$ + "]" + GJ_LIB_NL$
    NEXT i
    r$ = r$ + "}"
    dump_unsigned_bit_array$ = r$
END FUNCTION


''
' Returns string with a dump of an array of strings
'
' @param arr$() Array of strings to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_string_array$ (arr$(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING t, r
    lb = LBOUND(arr$) : ub = UBOUND(arr$) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "STRING ARRAY: " + label$ + "$(" + t$ + ") {" + GJ_LIB_NL$
    FOR i = lb TO ub
        r$ = r$ + "  (" + _TRIM$(STR$(i)) + "): " + CHR$(34) + arr$(i) + CHR$(34)
        r$ = r$ + " [" + t$ + "]" + GJ_LIB_NL$
    NEXT i
    r$ = r$ + "}"
    dump_string_array$ = r$
END FUNCTION


''
' Returns string with a dump of an array of integers
'
' @param arr%() Array of integers to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_integer_array$ (arr%(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING t, r, l, num
    lb = LBOUND(arr%) : ub = UBOUND(arr%) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "INTEGER ARRAY: " + label$ + "%(" + t$ + ") {" + GJ_LIB_NL$
    FOR i = lb TO ub
        num$ = _TRIM$(STR$(arr%(i)))
        r$ = r$ + "  (" + _TRIM$(STR$(i)) + "): " + num$ + GJ_LIB_NL$
    NEXT i
    r$ = r$ + "}"
    dump_integer_array$ = r$
END FUNCTION


''
' Returns string with a dump of an array of unsigned integers
'
' @param arr%() Array of unsigned integers to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_unsigned_integer_array$ (arr~%(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING r, t, num
    lb = LBOUND(arr~%) : ub = UBOUND(arr~%) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "INTEGER ARRAY: " + label$ + "~%(" + t$ + ") {" + GJ_LIB_NL$
    FOR i = lb TO ub
        num$ = _TRIM$(STR$(arr~%(i)))
        r$ = r$ + "  (" + _TRIM$(STR$(i)) + "): " + num$ + GJ_LIB_NL$
    NEXT i
    r$ = r$ + "}"
    dump_unsigned_integer_array$ = r$
END FUNCTION


''
' Returns a string containing a dump of an array of singles
'
' @param arr!() Array of singles to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_single_array$ (arr!(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING r, t, num
    lb = LBOUND(arr!) : ub = UBOUND(arr!) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "SINGLE ARRAY: " + label$ + "!(" + t$ + ") {" + GJ_LIB_NL$
    FOR i = lb TO ub
        num$ = _TRIM$(STR$(arr!(i)))
        r$ = r$ + "  (" + _TRIM$(STR$(i)) + "): " + num$ + GJ_LIB_NL$
    NEXT i
    r$ = r$ + "}"
    dump_single_array$ = r$
END FUNCTION


''
' Dumps an array of longs
'
' @param arr&() Array of longs to dump
' @param label$ Label to give the dump block
'
FUNCTION dump_long_array$ (arr&(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING r, t, num
    lb = LBOUND(arr&) : ub = UBOUND(arr&) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "LONG ARRAY: " + label$ + "&(" + t$ + ") {" + GJ_LIB_NL$
    FOR i = lb TO ub
        num$ = _TRIM$(STR$(arr&(i)))
        r$ = r$ + "  (" + _TRIM$(STR$(i)) + "): " + num$ + GJ_LIB_NL$
    NEXT i
    r$ = r$ + "}"
    dump_long_array$ = r$
END FUNCTION


''
' Dumps an array of doubles
'
' @param arr#() Array of doubles to dump
' @param label$ Label to give the dump block
'
FUNCTION dump_double_array$ (arr#(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING r, t, num
    lb = LBOUND(arr#) : ub = UBOUND(arr#) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "DOUBLE ARRAY: " + label$ + "#(" + t$ + ") {" + GJ_LIB_NL$
    FOR i = lb TO ub
        num$ = _TRIM$(STR$(arr#(i)))
        r$ = r$ + "  (" + _TRIM$(STR$(i)) + "): " + num$ + GJ_LIB_NL$
    NEXT i
    r$ = r$ + "}"
    dump_double_array$ = r$
END FUNCTION


''
' Returns string with a dump of an array of bytes
'
' @param arr%%() Array of bytes to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_byte_array$ (arr%%(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING r, t, num, si
    lb = LBOUND(arr%%) : ub = UBOUND(arr%%) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "BYTE ARRAY: " + label$ + "%%(" + t$ + ") {"
    FOR i% = lb TO ub
        si$ = _TRIM$(STR$(i%))
        IF LEN(si$) = 1 THEN si$ = "0" + si$
        num$ = _TRIM$(STR$(arr%%(i%)))
        IF SGN(arr%%(i%)) = 1 THEN
            IF LEN(num$) = 2 THEN 
                num$ = " 0" + num$
            ELSEIF LEN(num$) = 1 THEN 
                num$ = " 00" + num$
            END IF
        ELSEIF SGN(arr%%(i%)) = 0 THEN
            num$ = " 000"
        ELSE
            IF LEN(num$) = 3 THEN
                num$ = "-0" + MID$(num$, 2, 2)
            ELSEIF LEN(num$) = 2 THEN
                num$ = "-00" + MID$(num$, 2, 1)
            END IF
        END IF
        IF i% MOD 8 = 0 THEN
            r$ = r$ + GJ_LIB_NL$
            r$ = r$ + STRING$(4, " ") + si$ + ": "
        END IF
        r$ = r$ + num$ + " "
    NEXT i%
    r$ = r$ + GJ_LIB_NL$ + "}"
    dump_byte_array$ = r$
END FUNCTION


''
' Returns string with a dump of an array of unsigned bytes
'
' @param arr~%%() Array of unsigned bytes to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_unsigned_byte_array$ (arr~%%(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING r, t, num, si
    lb = LBOUND(arr~%%) : ub = UBOUND(arr~%%) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "UNSIGNED BYTE ARRAY: " + label$ + "~%%(" + t$ + ") {"
    FOR i% = lb TO ub
        si$ = _TRIM$(STR$(i%))
        IF LEN(si$) = 1 THEN si$ = "0" + si$
        num$ = _TRIM$(STR$(arr~%%(i%)))
        IF LEN(num$) = 2 THEN 
            num$ = "0" + num$
        ELSEIF LEN(num$) = 1 THEN 
            num$ = "00" + num$
        END IF
        IF i% MOD 16 = 0 THEN
            r$ = r$ + GJ_LIB_NL$
            r$ = r$ + STRING$(4, " ") + si$ + ": "
        END IF
        r$ = r$ + num$ + " "
    NEXT i%
    r$ = r$ + GJ_LIB_NL$ + "}"
    dump_unsigned_byte_array$ = r$
END FUNCTION


''
' Returns string with a dump of an array of unsigned bytes as hex
'
' @param arr~%%() Array of unsigned bytes to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_unsigned_byte_array_as_hex$ (arr~%%(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING r, t, num, si, h
    lb = LBOUND(arr~%%) : ub = UBOUND(arr~%%) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "UNSIGNED BYTE ARRAY: " + label$ + "~%%(" + t$ + ") {"
    FOR i% = lb TO ub
        si$ = _TRIM$(STR$(i%))
        IF LEN(si$) = 1 THEN si$ = "0" + si$
        h$ = HEX$(arr~%%(i%))
        IF LEN(h$) = 1 THEN h$ = "0" + h$
        num$ = _TRIM$(STR$(arr~%%(i%)))
        IF i% MOD 16 = 0 THEN
            r$ = r$ + GJ_LIB_NL$
            r$ = r$ + STRING$(4, " ") + si$ + ": "
        END IF
        r$ = r$ + h$ + " "
    NEXT i%
    r$ = r$ + GJ_LIB_NL$ + "}"
    dump_unsigned_byte_array_as_hex$ = r$
END FUNCTION


''
' Returns string with a dump of an array of unsigned bytes as ascii
'
' @param arr~%%() Array of unsigned bytes to dump
' @param label$ Label to give the dump block
' @return STRING dump block
'
FUNCTION dump_unsigned_byte_array_as_ascii$ (arr~%%(), label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING r, t, num, si, h, c
    lb = LBOUND(arr~%%) : ub = UBOUND(arr~%%) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "UNSIGNED BYTE ARRAY: " + label$ + "~%%(" + t$ + ") {"
    FOR i% = lb TO ub
        si$ = _TRIM$(STR$(i%))
        DO WHILE LEN(si$) < LEN(STR$(ub))-1
            si$ = "0" + si$
        LOOP
        h$ = HEX$(arr~%%(i%))
        IF LEN(h$) = 1 THEN h$ = "0" + h$
        num$ = _TRIM$(STR$(arr~%%(i%)))
        IF i% MOD 16 = 0 THEN
            r$ = r$ + GJ_LIB_NL$
            r$ = r$ + STRING$(4, " ") + si$ + ": "
        END IF
        r$ = r$ + h$ + " "
    NEXT i%
    r$ = r$ + GJ_LIB_NL$ + "ASCII:"
    FOR i% = lb TO ub
        si$ = _TRIM$(STR$(i%))
        DO WHILE LEN(si$) < LEN(STR$(ub))-1
            si$ = "0" + si$
        LOOP
        IF arr~%%(i%) < 33 OR arr~%%(i%) > 254 THEN
            c$ = ".."
        ELSE
            c$ = CHR$(arr~%%(i%)) + " "
        END IF
        IF i% MOD 16 = 0 THEN
            r$ = r$ + GJ_LIB_NL$
            r$ = r$ + STRING$(4, " ") + si$ + ": "
        END IF
        r$ = r$ + c$ + " "
    NEXT i%
    r$ = r$ + GJ_LIB_NL$ + "}"
    dump_unsigned_byte_array_as_ascii$ = r$
END FUNCTION


''
' Dumps a dictionary object and its contents
'
' @param dict$() Dictionary object to dump
' @param label$ Label to give the dump block
'
FUNCTION dump_dict$ (dict() AS Dict, label$)
    DIM AS INTEGER lb, ub, i
    DIM AS STRING r, t, num, si, q, skey, sval
    lb = LBOUND(dict) : ub = UBOUND(dict) : t$ = _TRIM$(STR$(ub - lb))
    r$ = GJ_LIB_NL$ + "DICT: " + label$ + "(" + t$ + ") {" + GJ_LIB_NL$
    q$ = CHR$(34)
    FOR i = lb TO ub
        skey$ = dict(i).key
        sval$ = dict(i).val
        r$ = r$ + "  (" + _TRIM$(STR$(i)) + ") " + q$ + skey$ + q$
        r$ = r$ + ": " + q$ + sval$ + q$ + GJ_LIB_NL$
    NEXT i
    r$ = r$ + "}"
    dump_dict$ = r$
END FUNCTION
