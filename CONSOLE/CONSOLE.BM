''
' QB64_GJ_LIB
' GRYMMJACK'S CONSOLE LIB
'
' CONSOLE object with debugging.
'
' USAGE:
'   Insert '$INCLUDE:'path_to_GJ_LIB/CONSOLE/CONSOLE.BI' at the top of file
'   Insert '$INCLUDE:'path_to_GJ_LIB/CONSOLE/CONSOLE.BM' at the bottom of file
' 
' @author Rick Christy <grymmjack@gmail.com>
'
$LET GJ_LIB_CONSOLE_INC_BM = 1

''
' Log a boxed message to console if DEBUGGING
' @param STRING msg message to send
' @param INTEGER kolor% color to use
'
SUB console.box(msg$, kolor%)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e, color_code, lines, intensity
        DIM AS INTEGER afg
        intensity$ = "0"
        e$ = CHR$(27)
        afg% = ANSI_COLOR%(kolor%)
        color_code$ = e$ + "["
        IF kolor% > 7 THEN intensity$ = "1"
        color_code$ = color_code$ + intensity$ + ";3"
        color_code$ = color_code$ + _TRIM$(STR$(afg%)) + "m"
        lines$ = "+" + STRING$(LEN(msg$)+2, "-") + "+"
        _ECHO color_code$ + lines$
        _ECHO "| " + msg$ + " |"
        _ECHO lines$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB


''
' Log a banner to console if DEBUGGING
' @param STRING msg message to send
' @param INTEGER kolor% color to use
'
SUB console.banner(msg$, kolor%)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e, color_code, intensity
        DIM AS INTEGER afg
        intensity$ = "0"
        e$ = CHR$(27)
        afg% = ANSI_COLOR(kolor%)
        msg$ = STR.replace$(msg$, "\n", CHR$(10), -1)
        msg$ = STR.replace$(msg$, "\t", CHR$(9), -1)
        color_code$ = e$ + "["
        IF kolor% > 7 THEN intensity$ = "1"
        color_code$ = color_code$ + intensity$ + ";3"
        color_code$ = color_code$ + _TRIM$(STR$(afg%)) + "m"
        _ECHO color_code$ + msg$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB


''
' Log to console if DEBUGGING
' @param STRING msg message to send
'
SUB console.log(msg$)
    $IF DEBUGGING = TRUE THEN
        _ECHO msg$
    $END IF
    msg$ = ""
END SUB


''
' Log to console as info if DEBUGGING
' @param STRING msg message to send
'
SUB console.info(msg$)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e
        e$ = CHR$(27)
        _ECHO e$ + "[1;36m" + msg$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB


''
' Log to console as warning if DEBUGGING
' @param STRING msg message to send
'
SUB console.warn(msg$)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e
        e$ = CHR$(27)
        _ECHO e$ + "[1;33m" + msg$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB


''
' Log to console as error if DEBUGGING
' @param STRING msg message to send
'
SUB console.error(msg$)
    $IF DEBUGGING = TRUE THEN
        DIM AS STRING e
        e$ = CHR$(27)
        _ECHO e$ + "[1;31m" + msg$ + e$ + "[0m"
    $END IF
    msg$ = ""
END SUB

$IF GJ_LIB_STRINGS_INC_BM = UNDEFINED THEN
'$INCLUDE:'../STRINGS/STRINGS.BM'
$END IF
