''
' QB64_GJ_LIB
' GRYMMJACK'S SYS LIB
'
' Contains misc. helpful utils/tools
'
' USAGE:
'   Insert '$INCLUDE:'path_to_GJ_LIB/SYS/SYS.BI' at the top of file
'   Insert '$INCLUDE:'path_to_GJ_LIB/SYS/SYS.BM' at the bottom of file
' 
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Opens a URL in the default web browser
'
' ex: open_url_in_browser "https://youtube.com/grymmjack"
'
' @param STRING url$ to open (any protocol just passes through)
'
SUB open_url_in_browser(url$)
    DIM AS STRING cmd
    DIM AS INTEGER ret
    $IF WINDOWS THEN
        cmd$ = "start " + url$
    $ELSEIF MAC THEN
        cmd$ = "open " + url$
    $ELSEIF LINUX THEN
        cmd$ = "xdg-open " + url$
    $END IF
    ret% = _SHELLHIDE(cmd$)
END SUB


''
' Get system information as a big string
'
' @return STRING of system information
'
FUNCTION sys_info$()
    DIM AS STRING sout, nl
    nl$ = CHR$(10) ' For some reason CHR$(13) won't combine strings
    sout$ = ""
    sout$ = sout$ + "QB64 " + _OS$ + nl$
    sout$ = sout$ + "          _CWD$: " + _CWD$ + nl$
    sout$ = sout$ + "     _STARTDIR$: " + _STARTDIR$ + nl$
    sout$ = sout$ + "       COMMAND$: " + COMMAND$ + nl$
    sout$ = sout$ + "SCREEN" + nl$
    sout$ = sout$ + "          Width: " + _TRIM$(STR$(_WIDTH)) + nl$
    sout$ = sout$ + "         Height: " + _TRIM$(STR$(_HEIGHT)) + nl$
    sout$ = sout$ + "              X: " + _TRIM$(STR$(_SCREENX)) + nl$
    sout$ = sout$ + "              Y: " + _TRIM$(STR$(_SCREENY)) + nl$
    sout$ = sout$ + "            BPP: " + _TRIM$(STR$(_PIXELSIZE)) + nl$
    sout$ = sout$ + "       _DISPLAY: " + _TRIM$(STR$(_DISPLAY)) + nl$
    sout$ = sout$ + "          _DEST: " + _TRIM$(STR$(_DEST)) + nl$
    sout$ = sout$ + "        _SOURCE: " + _TRIM$(STR$(_SOURCE)) + nl$
    sout$ = sout$ + "DESKTOP" + nl$
    sout$ = sout$ + "          Width: " + _TRIM$(STR$(_DESKTOPWIDTH)) + nl$
    sout$ = sout$ + "         Height: " + _TRIM$(STR$(_DESKTOPHEIGHT)) + nl$
    sout$ = sout$ + "FONTS" + nl$
    sout$ = sout$ + "     Font Width: " + _TRIM$(STR$(_FONTWIDTH)) + nl$
    sout$ = sout$ + "    Font Height: " + _TRIM$(STR$(_FONTHEIGHT)) + nl$
    sout$ = sout$ + "     _PRINTMODE: " + _TRIM$(STR$(_PRINTMODE)) + nl$
    sout$ = sout$ + "MISC" + nl$
    sout$ = sout$ + "     _CONTROLCHR: " + _TRIM$(STR$(_CONTROLCHR)) + nl$
    sout$ = sout$ + "          _BLINK: " + _TRIM$(STR$(_BLINK)) + nl$
    sout$ = sout$ + "   _DEFAULTCOLOR: " + _TRIM$(STR$(_DEFAULTCOLOR)) + nl$
    sout$ = sout$ + "_BACKGROUNDCOLOR: " + _TRIM$(STR$(_BACKGROUNDCOLOR)) + nl$
    sout$ = sout$ + "     _CLIPBOARD$:" + nl$
    sout$ = sout$ + _CLIPBOARD$ + nl$
    sys_info$ = sout$
END FUNCTION


''
' Get devices as a big string
'
' @return STRING of devices and button information
'
FUNCTION device_info$()
    DIM AS STRING sout, nl, device_name, snum_buttons
    DIM AS INTEGER i, num_buttons, device_count
    nl$ = CHR$(10) ' For some reason CHR$(13) won't combine strings
    sout$ = ""
    device_count% = _DEVICES
    FOR i% = 1 TO device_count%
        device_name$ = _DEVICE$(i%)
        num_buttons% = _LASTBUTTON(i%)
        snum_buttons$ = _TRIM$(STR$(num_buttons%))
        sout$ = sout$ + "DEVICE: " + device_name$ + " #BTN: " + snum_buttons$ + nl$
    NEXT i%
    device_info$ = sout$
END FUNCTION